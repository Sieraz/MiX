//===== rAthena Script =======================================
//= Healer
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Basic healer script.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.1 Aligned coordinates with @go.
//============================================================

-	script	Healer	-1,{


	.@Price = 0;	// ค่าใช้จ่ายในการรักษา
	.@Buffs = 1;	// บัฟผู้เล่นด้วย? (1: ใช่ / 0: ไม่)
	.@Delay = 0;	// ระยะเวลารักษาเป็นวินาที

	if (@HD > gettimetick(2))
		end;
	if (.@Price) {
		message strcharinfo(0), "ค่าใช้จ่ายในการรักษา " + callfunc("F_InsertComma",.@Price) + " Zeny";
		if (Zeny < .@Price)
			end;
		if (select("^0055FFรักษา^000000:^777777ยกเลิก^000000") == 2)
			end;
		Zeny -= .@Price;
	}
	specialeffect2 EF_HEAL2;
	percentheal 100,100;
	if (.@Buffs) {
		specialeffect2 EF_INCAGILITY;
		sc_start SC_INCREASEAGI,240000,10;
		specialeffect2 EF_BLESSING;
		sc_start SC_BLESSING,240000,10;
	}
	if (.@Delay)
		@HD = gettimetick(2) + .@Delay;
	end;
OnInit:
	waitingroom "[#] ฟื้นฟูพลังชีวิต",0;
end;
}


// Duplicates
//============================================================
morocc,164,97,4	duplicate(Healer)	Healer#mor	909
