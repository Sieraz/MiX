//===== rAthena Script ======================================= 
//= Ticket Refiner
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Refiner that uses +5~9/+11 refine tickets to refine
//= equipment with no chance of failure.
//= NOTE: This NPC is currently disabled on official servers.
//===== Additional Comments: =================================
//= 1.0 First version. [Euphy]
//= 1.1 Do not refine above ticket level. [Euphy]
//============================================================

// Main NPC :: safety_Ref_NPC
//============================================================
morocc,201,110,4	script	Refine Master	851,{
	disable_items;
	setarray .@cert_weapon[0],
		6864,	// Guarantee_Weapon_19Up
		6875,	// Guarantee_Weapon_18Up
		6874,	// Guarantee_Weapon_17Up
		6873,	// Guarantee_Weapon_16Up
		6872,	// Guarantee_Weapon_15Up
		6871,	// Guarantee_Weapon_14Up
		6870,	// Guarantee_Weapon_13Up
		6584,	// Guarantee_Weapon_12Up
		6238,	// Guarantee_Weapon_11Up
		6228,	// Guarantee_Weapon_9Up
		6229,	// Guarantee_Weapon_8Up
		6230,	// Guarantee_Weapon_7Up
		6231,	// Guarantee_Weapon_6Up
		6456;	// Guarantee_Weapon_5Up
	setarray .@cert_armor[0],
		6865,	// Guarantee_Armor_19Up
		6881,	// Guarantee_Armor_18Up
		6880,	// Guarantee_Armor_17Up
		6879,	// Guarantee_Armor_16Up
		6878,	// Guarantee_Armor_15Up
		6877,	// Guarantee_Armor_14Up
		6876,	// Guarantee_Armor_13Up
		6585,	// Guarantee_Armor_12Up
		6239,	// Guarantee_Armor_11Up
		6232,	// Guarantee_Armor_9Up
		6233,	// Guarantee_Armor_8Up
		6234,	// Guarantee_Armor_7Up
		6235,	// Guarantee_Armor_6Up
		6457;	// Guarantee_Armor_5Up
	setarray .@cert_level[0],19,18,17,16,15,14,13,12,11,9,8,7,6,5;
	.@size_cert = getarraysize(.@cert_weapon);

	for ( .@i = 0; .@i < .@size_cert; ++.@i ) {
		if (countitem(.@cert_weapon[.@i]) > 0 || countitem(.@cert_armor[.@i]) > 0) {
			.@check = 1;
			break;
		}
	}

	if (.@check == 0) {
		mes "[Refine Master]";
		mes "สวัสดีครับ!";
		mes "ว่าไง ?";
		mes "ฉันเป็นผู้เชี่ยวชาญ";
		mes "สำหรับปรับแต่งไอเทม,";
		mes "แต่ฉันไม่ทำงานแล้ว.";
		next;
		switch(select("ฉันจะไปตามทางของฉัน.:อืม... นี่ทำให้ฉันสงสัย.")) {
		case 1:
			mes "[Refine Master]";
			mes "ระวังตัวด้วย นักผจญภัย.";
			close;
		case 2:
			mes "[Refine Master]";
			mes "อันที่จริง บางครั้งฉันก็ให้บริการปรับแต่งสำหรับนักผจญภัยด้วย ^006400Refine Ticket^000000...";
			mes "บ๊าย บาย~!";
			close;
		}
	}
	emotion ET_SURPRISE;
	mes "[Refine Master]";
	mes "ว่าไงจ๊ะ!";
	mes "ฉันสามารถปรับแต่งไอเท็มได้ถึง ^006400ระดับเดียวกับตั๋วของคุณ^000000.";
	mes "คุณไม่ต้องกังวล! ไม่มีโอกาสที่จะทำลายไอเทมของคุณ";
	next;
	if(select("ไว้ฉันจะกลับมาอีกครั้ง.:อัพเกรดไอเทมของฉันด้วยตั๋ว.") == 1) {
		mes "[Refine Master]";
		mes "โอเค.";
		mes "คุณจะกลับมาเมื่อไหร่ก็ได้.";
		close;
	}
	mes "[Refine Master]";
	mes "คุณต้องการปรับแต่งอุปกรณ์ใด?";
	next;
	setarray .@position$[1],"Head upper","Armor","Left hand","Right hand","Robe","Shoes","Accessory 1","Accessory 2","Head middle","Head lower";
	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for ( .@i = 1; .@i <= 10; ++.@i )
		.@menu$ += (getequipisequiped(.@indices[.@i]) ? getequipname(.@indices[.@i]) : .@position$[.@i] + "- [Empty]") + ":";

	.@part = .@indices[ select(.@menu$) ];
	if (!getequipisequiped(.@part)) {
		mes "[Refine Master]";
		mes "คุณต้องติดตั้งไอเท็มที่คุณต้องการปรับแต่ง.";
		close;
	}
	if (!getequipisenableref(.@part)) {
		emotion ET_OTL;
		mes "[Refine Master]";
		mes "โอ้, ฉันขอโทษ.";
		mes "ไอเทมนี้ไม่สามารถปรับแต่งได้.";
		close;
	}
	.@refineitemid = getequipid(.@part); // save id of the item
	.@refinerycnt = getequiprefinerycnt(.@part); //save refinery count
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);
	.@itemtype = getiteminfo( .@refineitemid, ITEMINFO_TYPE );
	if( .@itemtype == IT_WEAPON ){
		switch( getequipweaponlv( .@part ) ){
			case 1:
			case 2:
			case 3:
			case 4:
				copyarray .@tickets[0], .@cert_weapon[0], .@size_cert;
				.@type$ = "Weapon";
				break;
			default:
				// TODO:
				close;
		}
	}else if( .@itemtype == IT_ARMOR ){
		switch( getequiparmorlv( .@part ) ){
			case 1:
				copyarray .@tickets[0], .@cert_armor[0], .@size_cert;
				.@type$ = "Armor";
				break;
			default:
				// TODO:
				close;
		}
	}else{
		// TODO:
		close;
	}
	.@check = 0;
	for ( .@i = 0; .@i < .@size_cert; ++.@i ) {
		if (countitem(.@tickets[.@i]) > 0) {
			.@check = 1;
			break;
		}
	}
	if (.@check == 0) {
		emotion ET_THINK;
		mes "[Refine Master]";
		mes "คุณต้องการอัพเกรด ^006400"+.@type$+"^000000, กรุณามาพร้อมกับ ^006400"+.@type$+" Refine Ticket^000000.";
		mes "แล้วพบกันใหม่!";
		close;
	}
	mes "[Refine Master]";
	mes "โปรดเลือก ^006400"+.@type$+" Refine Ticket^000000 ที่คุณต้องการใช้.";
	next;
	.@menu$ = "";
	for ( .@i = 0; .@i < .@size_cert; ++.@i )
		.@menu$ += getitemname(.@tickets[.@i]) + ":";
	.@select = select(.@menu$)-1;
	.@ticket_lv = .@cert_level[.@select];
	.@ticket_id = .@tickets[.@select];

	if (countitem(.@ticket_id) == 0) {
		emotion ET_QUESTION;
		mes "[Refine Master]";
		mes getitemname(.@ticket_id)+" ไม่อยู่ในกระเป๋าของคุณ คุณใส่ไว้ในที่คลังของคุณหรือไม่?";
		mes "กรุณาตรวจสอบอีกครั้ง.";
		mes "แล้วเจอกัน!";
		close;
	}
	if (getequiprefinerycnt(.@part) >= .@ticket_lv) {
		emotion ET_PROFUSELY_SWEAT;
		mes "[Refine Master]";
		mes "^8B4513ไอเทมชิ้นนี้ได้รับการอัพเกรดมากกว่าระดับตั๋วของคุณ.^000000";
		mes "โปรดมาพร้อมกับรายการที่ปรับแต่งน้อยกว่าตั๋วของคุณ.";
		close;
	}
	mes "[Refine Master]";
	mes "ฉันจะปรับแต่ง ^006400"+getequipname(.@part)+"^8B4513 up to the +"+.@ticket_lv+" ระดับ^000000 with ^006400"+getitemname(.@ticket_id)+"^000000.";
	mes "ดำเนินการต่อได้เลยใช่ไหม?";
	next;
	if(select("ไม่.:เอาเลยพวก.") == 1) {
		emotion ET_THINK;
		mes "[Refine Master]";
		mes "โอ้, คุณเปลี่ยนใจ.";
		mes "โอเค.";
		mes "จะกลับมาเมื่อไหร่ก็ได้.";
		close;
	}
	mes "[Refine Master]";
	mes "ยอดเยี่ยม.";
	mes "ตามที่ขอ!";
	mes "ฉันมีวิธีพิเศษของฉันในการปรับแต่ง...";
	mes ".......บู๊มมมมม!";
	specialeffect EF_SUI_EXPLOSION;
	if (countitem(.@ticket_id) < 1) {
		next;
		mes "Error!";
		mes "โปรดรายงานสิ่งนี้.";
		close;
	}
	delitem .@ticket_id,1;

	// anti-hack
	if (callfunc("F_IsEquipIDHack", .@part, .@refineitemid) ||
		callfunc("F_IsEquipRefineHack", .@part, .@refinerycnt) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3])) {
		mes "[Refine Master]";
		emotion ET_FRET;
		mes "รอสักครู่...";
		mes "คุณคิดว่าฉันโง่?!";
		mes "คุณเปลี่ยนของในขณะที่ฉันไม่ได้ดู! ออกไปจากที่นี่!";
		close;
	}
	successrefitem .@part, .@ticket_lv - getequiprefinerycnt(.@part);
	next;
	emotion ET_DELIGHT;
	mes "[Refine Master]";
	mes "เอาล่ะนี่คือ~";
	mes "สุดยอม, ^0000FF"+strcharinfo(0)+"^000000!";
	mes "ขอแสดงความยินดีกับการส่องแสงของคุณ "+.@type$+".";
	mes "คุณดูดี!";
	mes "ลาก่อน~!";
	close;
OnInit:
		waitingroom "[#] ตีบวกโดยใช้ตั๋ว",0;
	end;
}
